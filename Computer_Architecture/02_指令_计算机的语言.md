#  指令：计算机的语言

## MIPS-32 概述

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105115945638.png" alt="image-20250105115945638" style="zoom: 80%;" />

### 指令的组成——MIPS 的设计思想

计算机执行任何程序，本质上都是在执行机器语言**指令（instruction）**每条指令都是一条 0-1 串。

指令首先需明确执行何种操作，通常用 0-1 串的前几位来表示，通常称为**操作码**。

指令还要指出需要操作的数据来自哪里、操作后的结果数据放回哪里，此时通常用 0-1 串中的剩余位来表示，称为**操作数**或**地址码**。

大部分操作数都是一个地址编号，告诉 CPU 从哪里取得数据、向哪里放回数据，所以操作数通常也叫做地址码。

| 操作码 | 地址码 | 地址码 |
| :----: | :----: | :----: |

MIPS 作为一种 RISC 指令集，设计力求保证硬件设备的简单性。在 32 位 MIPS 汇编语言（MIPS-32）中，所有指令都是 32 位长。

### MIPS-32 中的通用寄存器

MIPS 中运算操作的操作数必须来自**寄存器（register）**或者指令本身。（*寄存器是一种位于 CPU、比 cache 更小更快的存储器，用来暂时存放运算的源数据和结果。*）

一些寄存器是专用的，如存放执行中指令的地址的**程序计数器（PC）**；与此相对应，用于暂时存放运算数据的寄存器称为通用寄存器。

MIPS 中一共有 32 个 32 位的寄存器，共 128B（大部分架构都采用 16 或 32 个寄存器）。

约定：

程序中的变量存放在**保存寄存器（store reg）**中：`$s0`~`$s7` 共 8 个。

运算的临时变量、中间变量存放在**临时寄存器（temp reg）**中：`$t0`~`$t7` 共 8 个。

还有一个**零寄存器**，永远存放 32 位的 0，写作 `$zero`。

## 三类汇编指令

### 算术运算：加 `add`、减 `sub`

C 赋值语句：

```c
c = a + b;
```

加法指令：将 `a` 和 `b` 中的数据相加，并将结果存放在 `c` 中。

```assembly
add c, a, b
```

再次强调：MIPS 中运算的操作数必须来自寄存器或者指令本身。

假设变量 a，b，c 分别存放在 `$s0`，`$s1`，`$s2` 中，这条指令就应当写为：

```assembly
add $s2, $s0, $s1
```

加法中两个加数可以对换，但减法不行，故 `c = a - b` 必须写作：

```assembly
sub $s2, $s0, $s1
```

运算的“原材料”，a 和 b 对应的寄存器 `$s0`，`$s1`，分别称为源操作数 1（src1）和源操作数 2（src2）运算的结果 c 对应的寄存器 `$s2` 称为目的操作数（des）。加减指令的通式：

```assembly
add/sub des, src1, src2
```

### 算术运算：加立即数 `addi`

在 `i++` 即 `i = i + 1`；这条赋值语句中，有个确定的常数 1，与其采取额外的步骤将 1 装入某个寄存器，不如让指令本身包含这个 1。

假设变量 `i` 位于寄存器 `$s0`，我们把加法指令的第二个源操作数改为常数 1：

```assembly
addi $s0, $s0, 1
```

此为加立即数（add immediate）指令。因为 `addi` 指令中的立即数可以取负数（对立即数取负后相加）。因此，MIPS 中没有 `subi` 指令。

### 逻辑按位运算：`and`、`or`、`nor` 指令

当两个源寄存器中，对应的位上同时为 1 时，与 `and` 操作结果为 1。

当两个源寄存器中，又对应的位上至少有一个为 1 时，或 `or`操作结果为 1。

因此，假设：

```assembly
$t0 = 0000 0000 0000 0000 0000 0000 0000 1001
$t1 = 0000 0000 0000 0000 0000 0000 0000 1100
```

<u>执行下列两条指令后，`$t2` 中的数据分别变为多少？</u>

```assembly
and $t2, $t0, $t1  # 0000 0000 0000 0000 0000 0000 0000 1000
or $t2, $t0, $t1   # 0000 0000 0000 0000 0000 0000 0000 1101
```

任何数据与 0 进行或非 `nor` 操作，都会 0/1 反转。

<u>执行下列指令后，`$t2` 中的数据会变为多少？</u>

```assembly
nor $t2, $t0, $zero  # 1111 1111 1111 1111 1111 1111 1111 0111
```

### 逻辑移位运算：`sll` 和 `srl` 指令

比较 12 和 120 两个十进制数，通过在最低位的右边添加一个 0，变为了 10 倍。

比较 11 和 110 两个二进制数，通过在最低位的右边添加一个 0，变为了多少倍？1100 呢？

**逻辑左移（shift left Iogic）**指令让寄存器中的数据整体往左移动指定的位数，并在右边空出来的位上补 0。（可以模拟乘 2 运算）

假设：

```assembly
$s2 = 0000 0000 0000 0000 0000 0000 0000 0101
```

逻辑左移两位后，放到寄存器 `$s0` 中：

```assembly
sll $s0, $s2, 2
```

这里的 2 不是 `addi` 指令中的立即数，而是告诉计算机移动几位的**移位量（shift amount）**。

通过这样一句指令，我们实际上完成了乘 4 的运算！

> `srl` 指令当然可以实现除 2 运算，使用场景不多，不额外讨论。

### 综合练习（一）

翻译一下 C 语句：

```c
result = a - 10 + (b + c * 5);
```

使用 `$s0` 表示 `a`，`$s1` 表示 `b`，`$s2` 表示 `c`，而 `$s3` 表示 `result`；则对应有：

```assembly
sll $t0, $s3, 2     # t0 = 4 * 2
add $t1, $t0, $s3   # t1 = 5 * c
add $t2, $t1, $t0   # t2 = b + 5 * c
addi $t3, $s0, -10  # t3 = a - 10
add $s3, $t3, $t2   # s3 = t2 + t3
```

### 寄存器-存储器数据传送：`lw` 指令

运算指令的操作数必须来自于寄存器/指令本身；但是，通用寄存器一共只有 128B；数组元素却可以占据成千上万个字节，只能存放在内存中。

这时，我们把数组第一个元素（`a[0]`）的 32 位地址，称为数组的**基址**，放在寄存器中。基址加上要找的元素的下标，就组成了这个元素的地址。

如果源操作数在内存中，是数组 a 的 5 号元素（第六个元素），数组 a 的基址存放 `$s1` 中，那么，`a[5]` 的地址就表示为 `5($s1)`。计算机会自动计算 `$s1` 中的基址和**偏移量** 5 的和，找到 `a[5]` 的地址。

将 `a[5]` 从内存传送到寄存器 `$s0`，使用**取字指令（load word）**：

```assembly
lw $s0, 5($s1)
```

（还是有问题，详见下一部分）

### 寄存器-存储器数据传送：字与 `sw` 指令

MIPS 的通用寄存器都是 32 位长。这个长度就是 MIPS 体系结构的**字长**，通常代表了参与运算的数据的长度
因此我们约定：此门课程中，`1 word = 32b = 4B`。

`a[5]` 相对于 `a[0]` ，在内存中的距离是 5 个字，而不是 5 个字节。又因为内存<u>按字节编址</u>，即，内存每个字节都有一个特定的编号；所以偏移量应该是 5 x 4 = 20 个字节，`a[5]` 的地址应表示成 `20($s1)`。于是取数指令变为：

```assembly
lw $s0, 20($s1)
```

如果我们要把 `$t0` 中的运算结果送回内存中的 `a[2]`，需要用到**存字指令（store word）**：

```assembly
SW $t0, 8($s1)
```

### 寄存器间数据传送、装载立即数到寄存器

如果我们需要把数从 `$t0` 保存到存放某变量的 `$s1` 中，怎么实现？

MIPS 没有专门的寄存器间移动数据的指令。但是，通过把源寄存器中的数据加上 0 再保存到目标寄存器中，可以实现相同的功能：

```assembly
addi $s1, $t0, 0
add $s1, $t0, $zero
```

这个功能可以用 **`move` 伪指令**来替代：

```assembly
move $s1, $t0
```

假如我们要把一个常数 10 装入寄存器 `$s2`，同样可以采用 `addi` 指令：

```assembly
add $s2, $zero, 10
```

或使用**取立即数（load immediate）**伪指令：

```assembly
li $s2, 10
```

### 装载 32 位立即数到寄存器

我们说可以用 `addi` 指令向寄存器装载立即数：`addi $s2，$zero，10`。

但是，`addi` 指令中的立即数 10 只能占用 32 位指令中的一部分；16 位只能表示 2E16，即六万多个数，寄存器却能容纳 2E32，即 40 多亿个数。

假设我们要向寄存器 `$s2` 装载一个 32 位的立即数：`10A2 7FFF`。

我们必须先用**取高位立即数（load upper immediate）**指令，把 `10A2` 放入 `$s2` 的高 `16` 位：

```assembly
lui $s2，4258  #十六进制的 10A2 等于十进制的 4258
```

执行完上述指令会将低 16 位置为 0，再让 `$s2` 与低 16 位的立即数 `7FFF` 进行或运算：

```assembly
ori $s2, $s2, 32767
```

这样，就分两步把 32 位立即数装载到了 32 的寄存器中。

> 不能使用 `addi` 替代 `ori` 指令，如果低 16 位的最高位是 1，`addi` 会把它理解为负数。

### 综合练习（二）

```C
a[i] = a[0] + 100000;
```

假设数组 a 的基址位于 `$s0`，变量 i 位于 `$s1`。

100000(10) = 186A0(16)，1(16) = 1(10)，86A0(16) = 34464(10)。

```assembly
lw $t0, 0($s0)
lui $t1, 1
ori $t1, $t1, 34464
add $t2, $t0, $t1 
sll $t3, $s1, 2 
add $t4, $s0, $t3
sw $t2, 0($t4)
```

### 决策：条件分支 `beq` 和 `bne`

最基础的判断条件是相等关系。假设：

```assembly
$s0 = 0, $s1 = 0, $s2 = 1
```

**相等则分支（branch if equal）指令**在两个源操作数寄存器中的值相同时分支，分支以**分支标签**表示：

```assembly
beq $s0, $s1, Label
```

与此相对应，**不等则分支（branch ifnotequal）指令**在值不同时分支标签：

```assembly
bne $s0, $s2, Label
```

如果不发生分支，则继续执行内存中相邻的下一条指令。

### 综合练习（三）

```c
if (i == j) {
  f = g + h;
} else {
  f = g - h;
}
```

假设 f、g、h、i、j 分别存放在 `$s0`~`$s4` 中。

```assembly
      bne $s3, $s4, Else
		  add $s0, $s1, $s2
		  j Exit
Else: sub $s0, $s1, $s2
Exit:
```

> **结论**：判定相等 `==` 使用 `bne`，判断不等 `!=` 使用 `beq`。

### 决策：小于则置位 `slt`

除了相等、不等关系，我们还经常比较两个数的大小。MIPS 有一条**小于则置位（set on less than）指令 `slt`**

>  置位：将一位设置为 1；复位：将一位设置为 0。

还是假设：

```assembly
$s0 = 0, $s1 = 0, $s2 = 1
```

有：

```assembly
slt $tO, $s0, $s2
```

源操作数 1 小于源操作数 2，此时把目的操作数寄存器 `$t0` 置位为 1。

```assembly
slt $tO, $sO, $s1
```

源操作数1 大于等于（即没有小于）源操作数 2，此时把目的操作数寄存器 `$t0` 复位为 0。

### 6 种条件判定及其伪指令

通过 `slt`、`beq`、`bne`（严格来说还有小于立即数则置位 `slti` 指令，不作讨论）指令的
各种组合，我们就能够实现全部六种比较条件，即六种值为真或假的布尔表达式。

```c
if (i < j) {
  f = g + h;
} else {
  f = g - h;
}
```

```assembly
      slt $t0, i, j         # 当 i < j 时，把 $t0 置为 1，否则为 0
      beq $t0, $zero, Else  # 当 $t0 为 0 时，执行 else 后的语句
      add f, g, h           # 否则顺着执行 if 后的语句
      j Exit                # 加法完成后退出 if-else 语句
Else: sub f, g, h   				# else
Exit:
```

> **结论**：判定大于 `>` 或小于 `<` 使用 `slt` 和 `beq`，判定大于等于 `>=` 或小于等于 `<=` 使用 `slt` 和 `bne` 。

对于比大小的四种比较条件，可以使用伪指令：

- 小于则分支：`blt`
- 大于则分支：`bgt`
- 小于等于则分支：`ble`
- 大于等于则分支：`bge`

### 综合练习（四）

```c
while (a[i] == k) {
  ++i;
}
```

假设 i，k 分别存放在 `$s3` 和 `$s5` 中，a 的基址存放在 `$s6` 中。

```assembly
Loop: sll $t0, $s3, 2  
      add $t1, $s6, $t0
      lw $t2, 0($t1)     # a[i] -> $t2
      bne $t2, $s5, Exit
			addi $s3, $s3, 1   # ++i
			j Loop
Exit:
```

### MIPS 汇编指令小结

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105144520189.png" alt="image-20250105144520189" style="zoom: 80%;" />

### 指令格式：R 型

指令中含<u>三个寄存器</u>的运算指令都属于 **R 型（register type）指令**。

```assembly
add/sub des, src1, src2 
add/or/nor des, src1, src2 
slt des, src1, src2
```

32 位的 MIPS 指令一共分为 6 个字段：

|  op  |  rs  |  rt  |  rd  | shant | funct |
| :--: | :--: | :--: | :--: | :---: | :---: |
| 6 位 | 5 位 | 5 位 | 5 位 | 5 位  | 6 位  |

- **op**：operation code，操作码；
- **rs**：register source，源操作数寄存器 → **rt**：表示第二个源操作数寄存器。
- **rd**：register destination，目的寄存器；
- **shamt**：shift amount，移位量；
- **funct**：function code，功能码。

---

R 型指令的操作码 `op` 都是 **6 个 0**，由 6 位功能码 `funct` 进一步指定执行什么操作。

<u>**`$t0`-`$t7` 分别为 8-15 号寄存器，`$s0`-`$s7` 分别为 16-23 号寄存器。**</u>

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105193337610.png" alt="image-20250105193337610" style="zoom:50%;" />

> `add` 和 `sub` 的操作码字段均为 0，而功能码字段前者为 32、后者为 34；因为不是移位指令，所以无需指定移位量，直接设置为 0。

因此上面的被翻译为：

```assembly
add $t0, $s1, $s2
```

反过来，`sub $s1, $s1, $s0` 翻译为：

```assembly
000000 10001 10000 10001 00000 100010 
```

此外，使用移位量的两条逻辑移位指令：

```assembly
sll/srl des, src1, shamt
```


也属于 R 型指令，因为没有第二个源操作数寄存器，`rt` 被置为 0。

### 指令格式：I 型（立即数）

有两条“目的 reg + 源 reg + 立即数”格式的指令：

```assembly
addi des, src1, i 
ori des, src1, i
```

通过把 R 型指令中的后三个字段拼接成一个 16 位的立即数字段，让指令本身包含常数这样的指令属于 I 型（immediate type）指令。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105194933272.png" alt="image-20250105194933272" style="zoom:67%;" />

以 `addi` 指令为例，__*其操作码为 8*__，由于 rd 字段被合并了，现在 rt 就成为目的寄存器。

```assembly
addi $t1, $t0, 15 
# 001000 01000 01001 0000000000001111
```

### 指令格式：I 型（偏移量）

```assembly
lw/sw reg, num(reg)
```

两条数据传送指令也包含两个寄存器和一个常数；同样属于 R 型指令。此时，16 位立即数字段的含义发生了改变，表示数组元素相对于数组基址的地址偏移量。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105195714375.png" alt="image-20250105195714375" style="zoom:67%;" />

无论是 `lw` 还是 `sw` 指令都是由 rs 字段表示的寄存器值与 address 字段相加，得到存储器单元地址 rt 字段表示与存储器单元交换数据的寄存器。

> `lw`、`sw` 指令操作码分别为 35 和 43。

```assembly
lw $t0, 8($s1)
# 100011 10001 01000 0000000000100
```

### 指令格式：I 型（标签）

```assembly
beg/bne src1, src2, Label
```

在这两条条件分支指令中，同样使用了两个寄存器；还有一个分支标签的地址，用 16 位立即数字段表示（也就变成了 Address 字段）也属于 I 型指令。

例如，当 i（`$s0`）和 j（`$s1`）相等时分支到地址为 10000 的标签 Else。

```assembly
beq $s0, $s1, Else
```

翻译为机器语言为（没有目的操作数，两个都是原操作数，直接顺序排下来）：

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105201447869.png" alt="image-20250105201447869" style="zoom:50%;" />

> 这里的 10000 实际上并不是 Else 标签指向指令的地址。

### 机器语言指令格式小结

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105201722967.png" alt="image-20250105201722967" style="zoom:67%;" />

`lui` 指令的指令格式不作讨论；五条伪指令本身不是真正的指令，程序运行时会被替换为真正指令，不讨论指令格式。

## 复习（一）

1. 指令通常由哪两个部分组成？MIPS-32 指令长度均为多少？
2. 8 个临时寄存器、8 个保存寄存器分别是什么编号？零寄存器存储什么？
3. 回顾综合练习1～4，掌握运算、数据传送、决策三类汇编指令，注意字和字节的区别；
4. 练习上一页 PPT 中五条指令（`add`、`sub`、`addi`、`lw`、`sw`）汇编语言和机器语言的转化。

## 过程支持

C 语言中的函数（一种典型的**过程**）是结构化编程的强大工具是结构化编程的强大工具；函数获取参数、执行运算、返回结果，就好比侦探拿着一份计划书去执行任务，再带来想要的结果。

1. 主程序（**调用者**）将参数放在过程（**被调用者**）可以取用的特定位置（什么位置？）；
2. 主程序将控制权交给过程；
3. 过程申请并获得存储资源；
4. 过程执行；
5. 过程将结果的值放在主程序可以取用的特定位置（什么位置？）；
6. 过程把控制权返还给主程序，执行调用过程指令的下一条指令（怎么找到这个位置？）。

### 支持过程的三大寄存器

- 4 个**参数寄存器（argument reg）**：`$a0`-`$a3`；
- 2 个**值寄存器（value reg）**：`$v0`、`$v1`；
- 1 个**返回地址寄存器（return address reg）**：`$ra`（主程序把下一条指令的 32 位地址存入）。

截至目前，学习了保存寄存器、临时寄存器各 8 个，零寄存器 1 个，以及这里的 7 个。共 24 个寄存器，占 MIPS-32 寄存器总数的四分之三。

> 主程序通过什么指令，可以跳转到过程指令，并把下一条指令的地址存入 `$ra`？

### `jal-jr` 指令对、程序计数器

跳转并链接（jump and link）指令可以同时实现两个功能：

1. 无条件跳转到一个标签；
2. 将下一条指令的地址放入返回地址寄存器 `$ra`。

```assembly
jal Label
```

**`jal` 指令由调用者主程序使用，还是由被调用者过程使用？**主程序使用。

寄存器跳转（jump reg）指令可以跳转到某一寄存器存储的 32 位地址，基本上只和返回地址寄存器搭配：

```assembly
jr $ra
```

**`jr` 指令由调用者主程序使用，还是由被调用者过程使用？**调用者过程使用。

### 综合练习（五）

数组清零的操作。

```c
void clear(int a[], int size) {
  for (i = 0; i < size; ++i) {
    a[i] = 0; 
  }
}
```

数组 a 的基址位于 `$a0` 中，而 size 默认存放在 `$a1`中，i 存放在 `$t0` 中。

```assembly
clear: add $t1, $zero, $zero  # i = 0
 Loop: sll $t1, $t0, 2       
       add $t2, $t1, $a0      # 此时 $t2 存放的就是地址
       sw $zero, 0($t2)       # 存放进去
       addi $t0, $t0, 1       # ++i
       slt $t3, $t0, $a1
       beq $t3, $zero, Exit
       j Loop
 Exit: jr $ra                 # 返回地址
```

### 保存寄存器的压栈和出栈、栈指针 `$sp`

在过程调用前，主程序往往已经将自己要用的变量放在了保存寄存器中；如果过程要使用保存寄存器，要把主程序已经使用的保存寄存器入栈。

栈在内存中以**高地址为栈底，低地址为栈顶**。即，栈从高地址向低地址“生长”。**栈指针（stack pointer）**永远指向栈顶。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105212442385.png" alt="image-20250105212442385" style="zoom:67%;" />

入栈时，先把 `$sp` 减去待保存的；保存寄存器个数的 4 倍为什么是 4 倍？因为一个字是 4 个字节。

再用 `sw` 将保存寄存器存入栈中（方向从栈底到栈顶）。

过程结束时把这些数据出栈、放回保存寄存器，供主程序继续使用，步骤正好相反。

### 综合练习（六）

```c
int cal(int g, int h, int i, int j) {
  int f;
  f = (g + h) - (i + j);
  return f;
}
```

假设 f 存储在 `$s0` 中。

```assembly
cal: addi $sp, $sp, -4    # 预留出存放的空间
     sw $s0, 0($sp)       # 将特定值存入其中
     add $t0, $a0, $a1    # g + h
     add $t1, $a2, $a3    # i + j
     sub $s0, $t0, $t1    # (g + h) - (i + j)
     add $v0, $s0, $zero
     lw $s0, 0($sp)       # 出栈
     addi $sp, $sp, 4     # 栈顶指针
     jr $ra
```

---

**优化**：

```assembly
cal: add $t0, $a0, $a1
     add $t1, $a2, $a3
     sub $v0, $t0, $t1 
     jr $ra
```

### 综合练习（七）

嵌套过程调用，数组求平方和（嵌套过程）。

```c
int sum_of_squares(int a[], int size) {
  int i = 0;
  int sum = 0;
  for (i = 0; i < size; ++i) {
    sum = sum + square(a[i]);
  }
  return sum;
}

int square(int a) {
  int square;
  square = a * a;
  return square;
}
```

i 存入 `$t0` 中，sum 存入 `$t1` 中，square 存入 `$t2` 中；a[] 存入 `$a0` 中，size 存入 `$a1` 中，a 存入 `$a2` 中。

```assembly
    ss: add $sp, $sp, -4       
        sw $ra, 0($sp)         # 将 $ra 存入
        add $t0, $zero, $zero  # i = 0
        add $t1, $zero, $zero  # sum = 0
        slt $t2, $t0, $a1      # 先进行一次判断，小于则置为 0
        beq $t2, $zero, Exit   # 跳转
  Loop: sll $t2, $t0, 2
        add $t2, $t2, $a0      # 地址
        lw $a2, 0($t2)         # $a2 = a[i]
        jal square             # 使用子程序
        add $t1, $t1, $v0
        add $t0, $t0, 1        # ++i
        slt $t2, $t0, $a1      # 小于则置为 0
        beq $t2, $zero, Exit   # 跳转
        j Loop
       
square: mul $v0, $a2, $a2      # 进行乘法操作
        jr $ra
        
  Exit: move $v0, $t1          # 将 sum 返回
        lw $ra, 0($sp)         # 将 $ra 取出
        add $sp, $sp, 4  
        jr $ra
```

### 过程帧与帧指针 `$fp`

为了标记运行中过程建立的栈，除了栈顶的栈指针 `$sp` 还可以加一个**帧指针（frame pointer）`$fp`** 指向栈底，即过程帧的第一个字 `$fp` 和 `$sp` 之间的空间由运行中过程使用称为**过程帧**，也叫**活动记录**。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105222624060.png" alt="image-20250105222624060" style="zoom:50%;" />

### 全局指针 `$gp` 程序的内存分配

为了便于寻找位置固定的数据（主程序使用的变量，以及声明为 `static` 的变量，统称静态变量）。使用一个固定指向*<u>静态数据区</u>*某一位置的全局指针**（global pointer）`$gp`**。

程序在内存中包含五段，地址从低到高分别为：

1. **保留段**；
2. **正文段（代码段）**，保存指令；
3. **静态数据段**，保存静态数据；
4. **动态数据段（堆）**，从低往高“生长"；
5. **栈**，从高往低“生长"。

栈和堆此消彼长，实现了内存空间的高效利用。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105222821980.png" alt="image-20250105222821980" style="zoom:50%;" />

### 需要压栈保存的寄存器

- **综合练习（六）**：我们默认**保存寄存器 `$s0`~`$s7`** 存放了主程序的变量；需要由过程开始时压栈保存，纟结束时出栈恢复；
- **综合练习（七）**：如果一个过程（外层函数）嵌套了其他过程（内层函数)；外层函数通过 `jal` 修改了**返回地址寄存器 `$ra`**，此时 `$ra` 指向外层函数 `jal` 的下一条指令，不再是外层函数的返回地址。**栈指针寄存器`$sp`**、栈中的内容（即**栈指针以上的栈**）也需要由过程保留，在 `addi` 栈指针、`sw` 入栈、`lw` 出栈的过程中即可保存。

> **结论**：任何过程须显式地压栈保存即将使用的保存寄存器 `$s0`～`$s7`（用哪几个存哪几个），外层嵌套过程须显式地压栈保存返回地址寄存器 `$ra`。

## 五种寻址方式

### 32 个通用寄存器及其编号

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105231416095.png" alt="image-20250105231416095" style="zoom: 80%;" />

### R 型：寄存器寻址

所有操作数都是寄存器的指令（如 R 型指令，其采用 3 个寄存器操作数），采用**寄存器寻址（register addressing）**，操作数个数从**一个到三个不等**。

R 型指令 ↔ 寄存器寻址。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105231903527.png" alt="image-20250105231903527" style="zoom:67%;" />

1. **运算指令**：`add`、`sub`、`and`、`or`、`nor`，5 条三寄存器操作数指令；
2. **运算指令**：`sll`、`srl`，2 条双寄存器操作数指令（rt 不使用置为 0，使用 shamt）；
3. **决策指令**：`slt`，1 条三寄存器操作数指令；
4. **决策指令**：`jr`，1 条单寄存器操作数指令。

### I 型：立即数寻址和基址偏移寻址

第三个操作数（第二个源操作数）是*常数*的指令采用**立即数寻址（immediate addressing）**，具体包括 `addi`，`ori` 两条指令。

> 其实还包括 `lui` 指令，这里不着重强调其指令格式。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105232829938.png" alt="image-20250105232829938" style="zoom:80%;" />

两条数据传送指令 `lw`、`sw`，将基址寄存器和偏移量相加的内存寻址方式称为**基址偏移寻址**；可单独称为基址寻址（base addressing）和偏移寻址（displacement addressing）。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105232941811.png" alt="image-20250105232941811" style="zoom:67%;" />

### I 型：PC 相对寻址字地址和字节地址

两条条件分支指令 `beq`、`bne`。在汇编语言中使用标签来表示分支的目标地址，标签翻译成机器语言其实是个整数；告诉计算机从当前指令的地址出发，到达分支目标地址的距离是多少；**程序计数器（program counter, PC）**中保存了*执行中指令的地址*；分支指令中的 16 位分支地址是一个二进制补码，可正可负，表示以 PC + 4 为基准相加的**字地址数目**（注意不是字节），叫做 **PC 相对寻址（PC-relative addressing）**。
$$
\text{分支 32 位地址} = \text{PC} + 4 + 字地址偏移量
$$
<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105233423789.png" alt="image-20250105233423789" style="zoom:67%;" />

###  J 型：伪直接寻址

**J 型指令**只需要操作码和目标地址两个字段，形式上最为简单。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105234024974.png" alt="image-20250105234024974" style="zoom:67%;" />

J 型指令 ↔ 伪直接寻址，包括 `j`、`jal` 两条。

> 寄存器跳转 `jr` 指令是 R 型指令。

直接寻址指的是指令中直接给出 32 位内存地址，但 J 型指令地址字段只有 26 位；因此，执行 J 型指令时，先将 26 位*字*地址左移两位（右侧补 0）形成 28 位*字节*地址；再和 PC 的高四位拼接成 32 位地址，这就是伪直接寻址（pseudodirect addressing）。

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250105234235352.png" alt="image-20250105234235352" style="zoom:50%;" />

分析：

```assembly
Loop: sll $t1, $s3, 2     # Temp reg $t1 = 4 * i
      add $t1, $t1, $s6   # $t1 = address of save[i]
      lw $t0, 0($t1)      # Temp reg $t0 = save[i]
      bne $t0, $s5, Exit  # go to Exit if save[i] != k
      addi $s3, $s3, 1    # i = i + 1
      j loop              # go to Loop
Exit:
```

询问 Exit 立即数字段是多少？可是每条指令为 1 个字（即 4 个字节），假设 Loop 标签处为 10000，则至 `bne $t0, $s5, Exit` 处，其地址为 10012。根据公式：
$$
\text{分支 32 位地址} = \text{PC} + 4 + 字地址偏移量
$$
进行加 4 操作后有 10016，此时到达 `addi $s3, $s3, 1` 指令处，距离 Exit 还有两个字。因此字地址偏移量为 2。因此对于分支指令，因其地址表示为字地址数目，因此直接填 2 即可。

### 寻址方式小结

1. **R 型的寄存器寻址**：操作数为 1 个/ 2 个/ 3 个寄存器的数据；

2. **I 型的立即数寻址**：`addi`、`lui`、`ori` 三条立即数指令，其中一个操作数是指令字段中的常数；

3. **I 型的基址偏移寻址**：`lw`、`sw` 两条访存指令，将 rs 中的基地址和偏移量直接相加，得到偏移地址；以 `lw $t0, 12($s0)` 为例，可知 `$s0` 存储着基地址，而 12 为偏移量。

4. **I 型的PC相对寻址**：`beq`、`bne` 两条条件分支指令。

   分支指令中的 **PC 相对地址**（可正可负的**字偏移量**），左移两位（x4）形成字节偏移量，再和 PC + 4 中的字节地址相加，形成分支目标地址。
   以地址为 1000（十进制）的 `beq reg1, reg2, 4` 为例，4 为字偏移量，转换为字节偏移量为 4 × 4 = 16，因此最终分支目标地址为 PC + 4 + 16 = 1020。

5. **J 型的伪直接寻址**：将 26 位字地址左移两位（x 4）形成 28 位字节地址，再和 PC（实际上也是 PC + 4，因为高位变化小）的高四位拼接成 32 位跳转目标地址。以 PC 高四位为 1010 的`j 0000 0000 0000 0000 0000 0000 01`为例，先左移两位形成 `0000 0000 0000 0000 0000 0000 0100`，之后再拼接上，有：`1010 0000 0000 0000 0000 0000 0000 0100`。

### 扩大分支与跳转的范围

PC 相对寻址以 PC + 4 为基准，**加上**一个可正可负的 16 位补码字地址寻址范围为：

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106010513931.png" alt="image-20250106010513931" style="zoom:67%;" />

因为 16 位补码的表示访问为 $[-2^{15},2^{15} - 1]$，其为字偏移量，乘 4 后即可得到字节偏移量；而 $2^{17}\text{B} = 2^{7} \cdot 2^{10} \text{B}= 128 \text{KB}$。

伪直接寻址用 PC 中当前指令地址的高四位**拼接**指令中的 26 位字地址，寻址范围为：

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106010836177.png" alt="image-20250106010836177" style="zoom:67%;" />

26 位的字地址对应 26 个字，因此有 28 个字节。地址相同的地址块，大小为 $2^{28} \text{B} = 2^8 \cdot 2^{20} \text{B} = 256 \text{MB}$。

> 在**相近的内存地址**中寻址利用了**加速大概率事件**这一设计思想。

要分支到更远距离，可以将 `beq`/`bne` 取反，下接一条可能绕过的 `j` 指令；要跳转到更远距离，可以先将 32 位地址装载到某临时寄存器，再用 `jr` 指令。

假设在寄存器 `$s0` 与寄存器 `$s1` 值相等时需要跳转，可以使用如下指令：

```assembly
beq $s0, $s1, L1
```

用两条指令替换上面的指令，以获得更远的转移距离。

```assembly
      bne $s0, $s1, exit
      j L1
exit:
```

此时还是相等时进行跳转，但是将跳转的工作从 PC 相对寻址变成了伪直接寻址（即 `j L1`），而伪直接寻址的寻址范围更大，所以获得更远的转移距离。

### MIPS 汇编指令小结

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106012551305.png" alt="image-20250106012551305" style="zoom:80%;" />

## C 程序执行过程

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106013153783.png" alt="image-20250106013153783" style="zoom:50%;" />

### C 语言的 4 个翻译层次

- **编译器**将高级语言文件（.c）翻译成汇编语言文件（.asm）;
- **汇编器**首先把伪指令替换为等价的真正指令，再将汇编语言翻译成机器语言目标文件（.obj）；
- **链接器**把目标文件和静态链接库（.lib）、动态链接库（.dll）拼接成可执行文件（.exe）；
- **加载器**将可执行文件放入内存，装载执行。

## 复习（二）

1. 为了实现过程调用，我们引入了哪三类寄存器和哪个指令对？
2. 三类寄存器分别存放什么？指令对中的两条指令分别由谁使用，完成什么功能？
3. 当过程要使用保存寄存器时，要进行什么操作？<u>默认保存寄存器被主程序使用，而过程要想使用，如果要使用，一定要有入栈保存，出栈恢复的操作。</u>
4. 为什么过程内部的变量优先使用临时寄存器？<u>减少对内存的操作，优先使用临时寄存器。</u>
5. 复习综合练习 5～7，熟悉 `for` 循环、清零、函数调用等常见的 C 语句翻译。

6. R 型指令和 I 型指令分别采用什么寻址方式？I 型有哪三种寻址方式？
7. PC 相对寻址和伪直接寻址为了扩大寻址范围，其地址代表什么单位？<u>使用字这个单位，意味着可以节省两位的空间来活得更大的寻址范围。</u>
8. PC 相对寻址和伪直接寻址分别怎样获得标签的真实地址？
9. 复习 MIPS 汇编指令，对应上每条指令的指令格式和寻址方式。
10. 运行 C 程序要经过哪四个步骤？

## 习题

### 教材习题

C 语句翻译成 MIPS 汇编代码：

```c
B[8] = A[i - j]
```

其中 f～j 对应 `$s0`～`$s4`，数组 A、B 的基址分别存放在 `$s6`，`$s7`。

```assembly
sub $t0, $s3, $s4  # $t0 = i - j
sll $t0, $t0, 2
add $t0, $s6, $t0  # 取出地址
lw $t1, 0($t0)     # $t0 = A[i - j]
sw $t1, 32($s7)    # B[8] = $t0
```

---

MIPS 程序片段翻译为 C 语句：

其中 f～j 对应 `$s0`～`$s4`，数组 A、B 的基址分别存放在 `$s6`，`$s7`。

```assembly
sll $t0, $s0, 2    # $t0 = f * 4
add $t0, $s6, $t0  # $t0 = &A[f]
sll $t1, $s1, 2    # $t1 = g * 4
add $t1, $s7, $t1  # $t1 = &B[g]
lw $s0, 0($t0)     # f = A[f]
addi $t2, $t0, 4   ## $t2 = &A[f] + 4 = &A[f + 1]
lw $t0, 0($t2)     ## $t0 = A[f + 1]
add $t0, $t0, $s0  ## $t0 = A[f] + A[f + 1]
sw $t0, 0($t1)     ## B[g] = A[f] + A[f + 1]
```

```c
B[g] = A[f] + A[f + 1]
```

---

二进制机器语言翻译成 MIPS 汇编语言：

```assembly
000000 10000 10000 10000 00000 100000
```

```assembly
add $s0, $s0, $s0
```

---

MIPS 汇编语言翻译成十六进制机器语言，并指出指令类型：I 型指令。

```assembly
sw $t1, 32($t2)
```

```assembly
101011 01010 01001 0000000000100000 
1010 1101 0100 1001 0000 0000 0010 0000
AD490020
```

---

`$t0` 中存放 0x00101000，执行下列指令后 `$t2` 存放的值变为多少？

```assembly
      slt $t2, $0, $t0
      bne $t2, $0, ELSE
      j DONE
ELSE: addi $t2, $t2, 2
DONE:
```

`t2` 变成了 3。

---

1. 将常数 0010 0000 0000 0001 0100 1001 0010 0100 装载到 `$t1`。

   ```assembly
   lui $t1, 8193
   ori $t1, $t1, 18724
   ```

2. 当前 PC 的值为 0x00000000，能否使用一条跳转指令跳转到 1 中的地址？

   跳转指令是伪直接寻址，可以访问到高四位相同的位置的值，因此不能。

3. 当前 PC 的值为 0x00000600 ，能否使用分支指支指令跳转到 1 中的地址？不能。

4. 当前 PC 的值为 0x1FFFF000，能否使用一条分支指令跳转到 1 中的地址？

   `(PC + 4) = 0x1FFFF004`，根据分支语句的访问范围，有 `[0x1FFDF004, 0x2001F000]`，而上述常数使用十六进制表现为 0x20014924，在访问范围内，因此可以访问。

### 川大真题

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106024945983.png" alt="image-20250106024945983" style="zoom:80%;" />

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106025604207.png" alt="image-20250106025604207" style="zoom:80%;" />



<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106030525974.png" style="zoom:80%;" />

<img src="https://leafalice-image.oss-cn-hangzhou.aliyuncs.com/img/image-20250106030830215.png" alt="image-20250106030830215" style="zoom: 67%;" />

---

```c
void clear(int array[], int size) {
  int i;
  for (i = 0; i < size; ++i) {
    array[i] = 0;
  }
}
```

整数 i 保存在 `$t0` 中：

```assembly
clear: add $t0, $zero, $zero 
 Loop: slt $t1, $t0, $a1
       beq $t1, $zero, Exit
       sll $t1, $t0, 2
       add $t2, $t1, $a2
       sw $zero, 0($t2)
       addi $t0, $t0, 1
       slt $t1, $t0, $a1
       beq $t1, $zero, Exit
       j Loop
 Exit: jr $ra
```

---

```c
void exchange(int v[], int i) {
  int temp;
  temp = v[i];
  v[i] = v[i + 1];
  v[i + 1] = temp;
}
```

```assembly
exchange: sll $t0, $a1, 2
          add $t1, $t0, $a0
          lw $t2, 0($t1)
          addi $t4, $t1, 4
          lw $t3, 0($t4)
          sw $t2, 0($t4)
          sw $t3, 0($t1)
          jr $ra
```

---

```c
int GetKeyIndex(int pArray[], int nCount, int nKey) {
  int i, nIndex = -1;
  for (i = 0; i < nCount; ++i) {
    if (pArray[i] == nKey) {
      nIndex = i;
      break;
    }
  }
}
```

```assembly
GetKeyIndex: add $t0, $zero, $zero  # i = 0
             addi $t1, $zero, -1    # nIndex = -1
             slt $t2, $t0, a1       # i < nCount
             beq $t2, $zero, Exit
       Loop: sll $t3, $t0, 2        
             add $t3, $t3, $a0
             lw $t4, 0($t3)         # $t4 = pArray[i]
             beq $t4, $a2, L1       # $t4 = nKey -> L1
             addi $t0, $t0, 1       # ++i
             slt $t2, $t0, a1       # i < nCount
             beq $t2, $zero, Exit
             j Loop                 # -> Loop
         L1: add $t1, $t0, $zero    # nIndex = i
       Exit: add $v0, $t1, $zero    # $v0 = nIndex
             jr $ra
```
